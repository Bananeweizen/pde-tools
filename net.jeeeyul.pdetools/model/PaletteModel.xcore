@GenModel(basePackage="net.jeeeyul.pdetools.icg.builder.model")
@Ecore(nsURI="http://jeeeyul.net/pde-tools/icg/palette", nsPrefix="p")
package palette

import org.eclipse.core.resources.IFile
import org.eclipse.core.resources.ResourcesPlugin
import org.eclipse.core.runtime.Path
import org.eclipse.core.resources.IFolder

type File wraps IFile create
{
	ResourcesPlugin::workspace.root.getFile(new Path(it) )
}
convert
{
	it.fullPath.toPortableString
}

type Folder wraps IFolder create
{ 
	ResourcesPlugin::workspace.root.getFolder(new Path(it))
}
convert
{
	it.fullPath.toPortableString
}

interface FieldNameOwner{
	String fieldName
}

class Palette extends FieldNameOwner
{
	container Palette parent opposite subPalettes
	
	/**
	 * 팔레트가 표현하는 워크스페이스상의 폴더.
	 */
	Folder folder
	
	contains Palette[] subPalettes opposite parent
	contains ImageFile[] imageFiles opposite parent
}

class ImageFile extends FieldNameOwner
{
	container Palette parent opposite imageFiles
	
	/**
	 * 워크스페이스 상의 실제 IFile
	 */
	File file
}